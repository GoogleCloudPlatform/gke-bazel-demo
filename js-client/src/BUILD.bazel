# Copyright 2018 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package(default_visibility = ["//visibility:public"])

load("@angular//:index.bzl", "ng_module")
load("@build_bazel_rules_typescript//:defs.bzl", "ts_config", "ts_devserver")

ts_config(
    name = "tsconfig-test",
    src = "tsconfig-test.json",
    deps = ["//js-client:tsconfig.json"],
)

ng_module(
    name = "src",
    srcs = glob(
        ["*.ts"],
        exclude = ["main.ts"],
    ),
    assets = [
        ":app.component.html",
        "//js-client/src/styles:main",
    ],
    tsconfig = ":tsconfig.json",
    deps = [
        "//js-client/src/hello-world",
        "//js-client/src/material",
        "//js-client/src/todos",
        "@angular//packages/core",
        "@angular//packages/router",
    ],
)

# This devserver is written in Go and is super-fast.
# It doesn't run any bundler or code splitter. Instead, it concatenates
# UMD JavaScript code on-the-fly in-memory.
# This scales really well for massive codebases.
ts_devserver(
    name = "devserver",
    # serve these files rooted at /
    additional_root_paths = [
        "npm/node_modules/zone.js/dist",
        "npm/node_modules/tslib",
        "npm/node_modules/@angular/material/prebuilt-themes",
    ],
    # Start from the development version of the main
    entry_module = "gke_bazel_example/js-client/src/main.dev",
    scripts = [
        ":module-id.js",
    ],
    # This is the URL we'll point our <script> tag at
    serving_path = "/bundle.min.js",
    # Serve these files in addition to the JavaScript bundle
    static_files = [
        "@npm//node_modules/zone.js:dist/zone.min.js",
        "@npm//node_modules/tslib:tslib.js",
        "@npm//node_modules/@angular/material:prebuilt-themes/deeppurple-amber.css",
        "//js-client/src/styles:main.css",
        "//js-client/src/images:image_assets",
        "index.html",
    ],
    # Tell Bazel to build the sources first
    deps = ["//js-client/src"],
)

load("@build_bazel_rules_nodejs//:defs.bzl", "nodejs_binary", "rollup_bundle")

rollup_bundle(
    name = "bundle",
    # These Angular routes may be lazy-loaded at runtime.
    # So we tell Rollup that it can put them in separate JS chunks
    # (code-splitting) for faster application startup.
    # In the future, we could automatically gather these from statically
    # analyzing the Angular sources.
    additional_entry_points = [
        "js-client/src/hello-world/hello-world.module.ngfactory",
        "js-client/src/todos/todos.module.ngfactory",
    ],
    entry_point = "js-client/src/main.prod",
    deps = [
        "//js-client/src",
    ],
)

# Copy zone.min.js to this package.
# Needed because the prodserver only loads static files that appear under this
# package.
genrule(
    name = "copy_zonejs",
    srcs = ["@npm//node_modules/zone.js:dist/zone.min.js"],
    outs = ["zone.min.js"],
    cmd = "cp $< $@",
)

# See comment for zonejs above
genrule(
    name = "copy_systemjs",
    srcs = ["@npm//node_modules/systemjs:dist/system.js"],
    outs = ["system.js"],
    cmd = "cp $< $@",
)

# See comment for zonejs above
genrule(
    name = "copy_material_theme",
    srcs = ["@npm//node_modules/@angular/material:prebuilt-themes/deeppurple-amber.css"],
    outs = ["deeppurple-amber.css"],
    cmd = "cp $< $@",
)

load("@build_bazel_rules_nodejs//:defs.bzl", "history_server")

# Run a production server with history-server
history_server(
    name = "prodserver",
    data = [
        "index.html",
        ":bundle",
        ":copy_material_theme",
        ":copy_systemjs",
        ":copy_zonejs",
        "//js-client/src/images:image_assets",
        "//js-client/src/styles:main",
    ],
)

####

load("@io_bazel_rules_docker//nodejs:image.bzl", "nodejs_image")

# Create a NodeJS image that runs history-server as the prod server
nodejs_image(
    name = "angular_image",
    args = [""],
    data = [
        "index.html",
        ":bundle",
        ":copy_material_theme",
        ":copy_systemjs",
        ":copy_zonejs",
        "//js-client/src/styles:main",
        "//js-client/src/images:image_assets",
    ],
    entry_point = "history-server/modules/cli.js",
    node_modules = "@npm//:node_modules",
    templated_args = ["js-client/src"],
)
